.model small
.stack 100h

.data
    ; --- Data Segment ---
    pin_stored  db '1234'
    pin_input   db 4 dup(?)
    balance     dw 5000 
    
    ; --- Messages ---
    msg_welcome db 10, 13, '=== ATM Project By Amr Helal ( Your money in a save Place) ===$'
    msg_pin     db 10, 13, 'Enter PIN: $'
    msg_fail    db 10, 13, 'Wrong PIN! System Locked.$'
    
    msg_menu    db 10, 13, 10, 13, '1. Check Balance', 10, 13, '2. Deposit', 10, 13, '3. Withdraw', 10, 13, '4. Exit', 10, 13, 'Select > $'
    msg_wrong_input db 10, 13, 'Wrong Input! Please select 1, 2, 3 or 4.$'
    
    msg_bal     db 10, 13, 'Your Balance: $'
    msg_dep     db 10, 13, 'Deposit Amount: $'
    msg_with    db 10, 13, 'Withdraw Amount: $'
    msg_err     db 10, 13, 'Insufficient Funds , Your are Poor!$'
    msg_pos     db 10, 13, 'Must be positive Number!$'  
    msg_done    db 10, 13, 'Transaction Complete.$'
    msg_bye     db 10, 13, 'Good Bye! , Please Give me Full Mark:) $'

.code

; =================================================================
; MAIN 
; =================================================================
main proc far
    .startup            
   
   
    mov ax, @data
    mov es, ax
    assume es:@data

   
    call AUTH_USER      

  
app_loop:
    call SHOW_MENU      
    
    mov ah, 01h         
    int 21h
    
    cmp al, '1'
    je go_balance
    cmp al, '2'
    je go_deposit
    cmp al, '3'
    je go_withdraw
    cmp al, '4'
    je go_exit
    
    jmp go_wrong      

go_balance:
    call OP_BALANCE     
    jmp app_loop

go_deposit:
    call OP_DEPOSIT     
    jmp app_loop

go_withdraw:
    call OP_WITHDRAW    
    jmp app_loop

go_exit:
    call OP_EXIT   
    
go_wrong:
    lea dx, msg_wrong_input
    mov ah, 09h     
    int 21h
    jmp app_loop        
    
main endp

; =================================================================
;  AUTH_USER
; =================================================================
AUTH_USER proc near

    lea dx, msg_welcome
    mov ah, 09h    
    int 21h

    lea dx, msg_pin
    mov ah, 09h
    int 21h

    mov cx, 4 
    lea si, pin_input 
auth_loop:
    mov ah, 01h
    int 21h
    mov [si], al 
    inc si 
    loop auth_loop 

    lea si, pin_stored
    lea di, pin_input
    mov cx, 4 
    repe cmpsb  
    je auth_ok   
    
    
    lea dx, msg_fail
    mov ah, 09h
    int 21h
    
    .exit 1    
    
auth_ok:
    ret                 
AUTH_USER endp

; =================================================================
; SHOW_MENU
; =================================================================
SHOW_MENU proc near
    lea dx, msg_menu
    mov ah, 09h
    int 21h
    ret
SHOW_MENU endp

; =================================================================
;  OP_BALANCE
; =================================================================
OP_BALANCE proc near
    lea dx, msg_bal
    mov ah, 09h
    int 21h
    
    mov ax, balance
    call PRINT_NUM      
    ret
OP_BALANCE endp

; =================================================================
;  OP_DEPOSIT
; =================================================================
OP_DEPOSIT proc near
    lea dx, msg_dep
    mov ah, 09h
    int 21h
    
    call READ_NUM     ; return the number entered in ax   
    
    cmp ax, 0   
    jle dep_invalid  ; if equla or less than zero jump  
    ;zf=1 or  sf <> of 
    
    add balance, ax     
    
    lea dx, msg_done
    mov ah, 09h
    int 21h
    ret

dep_invalid:
    lea dx, msg_pos
    mov ah, 09h
    int 21h
    ret
OP_DEPOSIT endp

; =================================================================
; OP_WITHDRAW
; =================================================================
OP_WITHDRAW proc
    lea dx, msg_with
    mov ah, 09h
    int 21h
    
    call READ_NUM   ;return the number entered in ax   
    
    cmp ax, 0
    jle with_invalid
    
    mov bx, ax          
    mov ax, balance
    cmp ax, bx
    jl with_funds_err   ; if ax lower jump to no money 
    ; zf <> cf 
    
    sub balance, bx     
    
    lea dx, msg_done
    mov ah, 09h
    int 21h
    ret

with_funds_err:
    lea dx, msg_err
    mov ah, 09h
    int 21h
    ret

with_invalid:
    lea dx, msg_pos
    mov ah, 09h
    int 21h
    ret
OP_WITHDRAW endp

; =================================================================
; OP_EXIT
; =================================================================
OP_EXIT proc
    lea dx, msg_bye
    mov ah, 09h
    int 21h
    
    .exit 0           
    ret
OP_EXIT endp

; =================================================================
; READ_NUM
; =================================================================
READ_NUM proc
    push bx
    push cx         
    push dx
    push si 

    xor si, si      
    xor cx, cx      

   
    mov ah, 01h
    int 21h

    cmp al, '-'     
    jne check_digit 
    
    mov cx, 1       
    jmp read_next   

check_digit:
    
    jmp process_char 

read_next:
    mov ah, 01h
    int 21h

process_char:
    cmp al, 13          
    je check_sign_end
    cmp al, ' '        
    je check_sign_end
    
    cmp al, '0'
    jb read_next        
    cmp al, '9'
    ja read_next

   
    sub al, '0'         
    xor ah, ah
    push ax             

    mov ax, si
    mov bx, 10
    imul bx             
    mov si, ax
    
    pop ax
    add si, ax       
    jmp read_next

check_sign_end:
    mov ax, si          
    cmp cx, 1           
    jne rn_done
    neg ax             

rn_done:
    pop si
    pop dx
    pop cx
    pop bx
    ret
READ_NUM endp

; =================================================================
; PRINT_NUM 
; =================================================================
PRINT_NUM proc
    push bx
    push cx
    push dx

    cmp ax, 0
    jne pn_start
    mov dl, '0'
    mov ah, 02h
    int 21h
    jmp pn_done

pn_start:
    mov bx, 10
    xor cx, cx

pn_calc:
    xor dx, dx
    div bx
    push dx
    inc cx
    cmp ax, 0
    jne pn_calc

pn_print:
    pop dx
    add dl, '0'
    mov ah, 02h
    int 21h
    loop pn_print

pn_done:
    pop dx
    pop cx
    pop bx
    ret
PRINT_NUM endp
end    
